100 !*** ROF IS MAX. NO. ***
110 ROF=500
120 !*** RECORDS ON FILE ***
130 !***********************
140 !**   LIBRARY MASTER  **
150 !**   VERSION 3.1.2   **
160 !**      MAY 2013     **
170 !** FOR TI EXT.BASIC  **
180 !**  BY ROBERT LOCK   **
190 !**    ROBERTLM78     **
200 !***********************
210 CALL CLEAR
220 !**********************
230 !*** SETTING COLORS ***
240 !**********************
250 CALL SCREEN(8)
260 FOR CS=1 TO 12
270 CALL COLOR(CS,15,2)
280 NEXT CS
290 CALL COLOR(0,8,1)
300 !********************
310 !*** TITLE SCREEN ***
320 !********************
330 RESTORE 350
340 READ LM3$,VER$,BY$,CONT$
350 DATA LIBRARY MASTER,VERSION 3.1.2,BY ROBERT LOCK,PRESS SPACE TO CONTINUE
360 DISPLAY AT(7,8):LM3$: :TAB(8);VER$: : : : : : : : : : : :TAB(8);BY$:TAB(1):TAB(3);CONT$
370 CALL KEY(0,K,S)
380 IF S<>-1 THEN 370
390 IF K=32 THEN 440
400 IF K<>32 THEN 370
410 !******************
420 !*** MAIN MENU ****
430 !******************
440 CALL CLEAR
450 RESTORE 470
460 READ SM$,BA$,BT$,BS$,UD$,Q$
470 DATA SEARCH METHOD,1 BY AUTHOR,2 BY TITLE,3 BY SUBJECT,4 UPDATE DATA FILE,5 QUIT
480 DISPLAY AT(3,9):SM$: : :TAB(4);BA$: :TAB(4);BT$: :TAB(4);BS$: :TAB(4);UD$: :TAB(4);Q$
490 LET U$="0000FFFF00000000"
500 CALL COLOR(13,14,2)
510 CALL CHAR(128,U$)
520 CALL HCHAR(4,11,128,13)
530 CALL KEY(0,K,S)
540 IF K=49 THEN 650
550 IF K=50 THEN 1060
560 IF K=51 THEN 1490
570 IF K=52 THEN 2390
580 IF K=53 THEN 2990
590 IF K<49 THEN 530
600 IF K>53 THEN 530
610 IF S<>-1 THEN 530
620 !*********************
630 !*** AUTHOR SEARCH ***
640 !*********************
650 CALL CLEAR
660 RESTORE 680
670 READ AUTHOR$,AN$
680 DATA SEARCH BY AUTHOR,ENTER AUTHOR'S LAST NAME
690 BK$="PRESS 'BACK' TO SEARCH MENU"
700 CLRSC$="PRESS'DEL'TO CLEAR SCREEN"
710 EEIC$="-ENTER THE CORRECT CRITERIA FOR THE SEARCH MODE YOU ARE IN"
720 CHKSP$="-USE CORRECT SPELLING"
730 SGTN$="SUGGESTIONS:"
740 NPTL$="NOTE: DO NOT USE PUNCTUATIONIN TITLE ENTRY AND ENTER AS MUCH OF TITLE AS POSSIBLE"
750 PKNUM$="PRESS NUMBER DESIRED"
760 DISPLAY AT(3,7):AUTHOR$: : : :TAB(3);AN$: : : : : : : : : : : : : :;" ";CLRSC$:TAB(3);BK$:
770 CALL HCHAR(4,9,128,16)
780 DISPLAY AT(11,1):SGTN$: :CHKSP$:EEIC$
790 CALL KEY(0,K,S)
800 IF K=15 THEN 440
810 IF K=3 THEN GOSUB 3070
820 IF S<>-1 THEN 790
830 ACCEPT AT(9,3):SA$
840 IF LEN(SA$)=0 THEN 790
850 OPEN #1:"DSK1.LIBCAT",RELATIVE,INTERNAL,INPUT ,FIXED 108
860 FOR I=1 TO ROF
870 INPUT #1,REC I:AT1$,AT2$,AT3$,TT$,SBJ$,NT$
880 IF AT1$=SA$ THEN GOSUB 950
890 IF AT2$=SA$ THEN GOSUB 950
900 IF AT3$=SA$ THEN GOSUB 950
910 IF EOF(1)THEN CLOSE #1 :: GOTO 790
920 NEXT I
930 CLOSE #1
940 GOTO 790
950 DISPLAY AT(11,1):AT1$:AT2$:AT3$: :TT$:SBJ$:NT$
960 DISPLAY AT(20,22):"REC";I
970 CALL KEY(0,K,S)
980 IF K=12 THEN RETURN
990 IF K=15 THEN CLOSE #1 :: GOTO 410
1000 IF K=14 THEN CLOSE #1 :: GOTO 790
1010 IF S<>-1 THEN 970
1020 GOTO 970
1030 !********************
1040 !*** TITLE SEARCH ***
1050 !********************
1060 CALL CLEAR
1070 RESTORE 1090
1080 READ TITLE$,TTSCH$
1090 DATA SEARCH BY TITLE,ENTER TITLE (28 CHARS MAX)
1100 BK$="PRESS 'BACK' TO SEARCH MENU"
1110 CLRSC$="PRESS'DEL'TO CLEAR SCREEN"
1120 EEIC$="-ENTER THE CORRECT CRITERIA FOR THE SEARCH MODE YOU ARE IN"
1130 CHKSP$="-USE CORRECT SPELLING"
1140 SGTN$="SUGGESTIONS:"
1150 NPTL$="NOTE: DO NOT USE PUNCTUATIONIN TITLE ENTRY AND ENTER AS MUCH OF TITLE AS POSSIBLE"
1160 PKNUM$="PRESS NUMBER DESIRED"
1170 SPC$="PRESS SPACE TO CONTINUE"
1180 DISPLAY AT(3,7):TITLE$: : : :;" ";TTSCH$: : : : : : : : : : : : : :;" ";CLRSC$:TAB(3);BK$:
1190 CALL HCHAR(4,9,128,15)
1200 DISPLAY AT(11,1):NPTL$: :" ";SPC$
1210 CALL KEY(0,P,R)
1220 IF R<>-1 THEN 1210
1230 IF P=32 THEN 1250
1240 IF P<>32 THEN 1210
1250 DISPLAY AT(11,1):SGTN$: :CHKSP$:EEIC$
1260 CALL KEY(0,K,S)
1270 IF K=15 THEN 440
1280 IF K=3 THEN GOSUB 3070
1290 IF S<>-1 THEN 1260
1300 ACCEPT AT(9,1):TS$
1310 IF LEN(TS$)=0 THEN 1260
1320 IF LEN(TS$)=0 THEN 1260
1330 STS$=SEG$(TS$,1,27)
1340 OPEN #1:"DSK1.LIBCAT",RELATIVE,INTERNAL,INPUT ,FIXED 108
1350 FOR I=0 TO ROF
1360 INPUT #1,REC I:AT1$,AT2$,AT3$,TT$,SBJ$,NT$
1370 CTTS$=SEG$(TT$,1,27)
1380 IF EOF(1)THEN CLOSE #1 :: GOTO 1260
1390 IF CTTS$=STS$ THEN 1420
1400 NEXT I :: CLOSE #1
1410 GOTO 1260
1420 CLOSE #1
1430 DISPLAY AT(11,1):AT1$:AT2$:AT3$: :TT$:SBJ$:NT$
1440 DISPLAY AT(20,22):"REC";I
1450 GOTO 1260
1460 !**********************
1470 !*** SUBJECT SEARCH ***
1480 !**********************
1490 CALL CLEAR
1500 RESTORE 1520
1510 READ SUB$,ESUB$
1520 DATA SEARCH BY SUBJECT,ENTER SUBJECT
1530 BK$="PRESS 'BACK' TO SEARCH MENU"
1540 CLRSC$="PRESS'DEL'TO CLEAR SCREEN"
1550 SRCH$="YOU'RE SEARCH FOUND "
1560 MTC$=" MATCHES"
1570 OP$="ON "
1580 PG$=" PAGES"
1590 PRCD$="PRESS 'PROC'D' TO CONTINUE"
1600 PRNM$="PRESS PAGE NUMBER DESIRED"
1610 AID$="PRESS 'AID' TO VIEW SUBJECTS"
1620 EEIC$="-ENTER THE CORRECT CRITERIA FOR THE SEARCH MODE YOU ARE IN"
1630 ETRZ$="ENTER ZERO TO CHANGE PAGES"
1640 BEGS$="PRESS'BEGIN'INSTEAD OF PAGE NUMBER TO START NEW SEARCH"
1650 CHKSP$="-USE CORRECT SPELLING"
1660 SGTN$="SUGGESTIONS:"
1670 NPTL$="NOTE: DO NOT USE PUNCTUATIONIN TITLE ENTRY AND ENTER AS MUCH OF TITLE AS POSSIBLE"
1680 PKNUM$="PRESS NUMBER DESIRED: "
1690 DISPLAY AT(3,7):SUB$: : : :TAB(9);ESUB$: : : : : : : : : : : :AID$: :;" ";CLRSC$:TAB(3);BK$:
1700 CALL HCHAR(4,9,128,17)
1710 DISPLAY AT(11,1):SGTN$: :CHKSP$:EEIC$
1720 CALL KEY(0,K,S)
1730 IF K=15 THEN 440
1740 IF K=3 THEN GOSUB 3070
1750 IF K=1 THEN 1930
1760 IF S<>-1 THEN 1720
1770 ACCEPT AT(9,3):BS$
1780 IF BS$="REL" THEN 2200
1790 IF BS$="SCI" THEN 2200
1800 IF BS$="HST" THEN 2200
1810 IF BS$="POL" THEN 2200
1820 IF BS$="MUS" THEN 2200
1830 IF BS$="PTY" THEN 2200
1840 IF BS$="ART" THEN 2200
1850 IF BS$="HF" THEN 2200
1860 IF BS$="FIC" THEN 2200
1870 IF BS$="REF" THEN 2200
1880 IF BS$="TXT" THEN 2200
1890 IF BS$="GNF" THEN 2200
1900 IF BS$="IRE" THEN 2200
1910 IF BS$="SCT" THEN 2200
1920 GOTO 1720
1930 HST$="HISTORY (HST)"
1940 POL$="POLITICS (POL)"
1950 ART$="ART"
1960 MUS$="MUSIC (MUS)"
1970 SCI$="SCIENCE (SCI)"
1980 NNF$="NON-FICTION (GNF)"
1990 REF$="REFERENCE / TEXT (REF / TXT)"
2000 SCT$="SCOTLAND (SCT)"
2010 IRE$="IRELAND (IRE)"
2020 FIC$="FICTION (FIC)"
2030 PTY$="POETRY (PTY)"
2040 REL$="RELIGION (REL)"
2050 HFT$="HISTORICAL FICTION (HF)"
2060 DISPLAY AT(11,1):"USE ARROW KEYS TO NAVIGATE LIST.  PRESS'PROC'D'TO CONT"
2070 CALL KEY(0,PR,PRD)
2080 IF PR=12 THEN 2110
2090 IF PR>12 THEN 2070
2100 IF PR<12 THEN 2070
2110 CALL KEY(0,SB,SL)
2120 IF SB=11 THEN 2160
2130 IF SB=10 THEN 2180
2140 IF SB=14 THEN GOSUB 3040 :: GOTO 1720
2150 IF SL<>-1 THEN 2110
2160 DISPLAY AT(11,1):HST$:POL$:ART$:MUS$:SCI$:NNF$:REF$
2170 GOTO 2110
2180 DISPLAY AT(11,1):IRE$:SCT$:REL$:FIC$:HFT$:PTY$
2190 GOTO 2110
2200 OPEN #1:"DSK1.LIBCAT",RELATIVE,INTERNAL,INPUT ,FIXED 108
2210 FOR I=1 TO ROF
2220 INPUT #1,REC I:AT1$,AT2$,AT3$,TT$,SBJ$,NT$
2230 IF SBJ$=BS$ THEN GOSUB 2280
2240 IF EOF(1)THEN CLOSE #1 :: GOTO 1720
2250 NEXT I
2260 CLOSE #1
2270 GOTO 1720
2280 DISPLAY AT(11,1):AT1$:AT2$:AT3$: :TT$:SBJ$:NT$
2290 DISPLAY AT(20,22):"REC";I
2300 CALL KEY(0,K,S)
2310 IF K=12 THEN RETURN
2320 IF K=15 THEN CLOSE #1 :: GOTO 410
2330 IF K=14 THEN CLOSE #1 :: GOTO 1720
2340 IF S<>-1 THEN 2300
2350 GOTO 2300
2360 !**********************
2370 !** UPDATE DATA FILE **
2380 !**********************
2390 CALL CLEAR
2400 CALL COLOR(14,8,8)
2410 G$="FFFFFFFFFFFFFFFF"
2420 CALL CHAR(136,G$)
2430 RESTORE 2450
2440 READ UDDF$
2450 DATA UPDATE DATA FILE
2460 DISPLAY AT(3,7):UDDF$
2470 CALL VCHAR(1,1,136,24)
2480 CALL VCHAR(1,2,136,24)
2490 CALL VCHAR(1,31,136,24)
2500 CALL VCHAR(1,32,136,24)
2510 CALL HCHAR(4,9,128,16)
2520 INPUT "PASSWORD: ":PW$
2530 !*** PASSWORD ***
2540 IF PW$="PASSWORD" THEN 2570
2550 !****************
2560 GOTO 2520
2570 CALL CLEAR
2580 CALL VCHAR(1,1,136,24)
2590 CALL VCHAR(1,2,136,24)
2600 CALL VCHAR(1,32,136,24)
2610 CALL VCHAR(1,31,136,24)
2620 INPUT "ENTER FIRST RECORD NO: ":FRNM
2630 INPUT "ENTER LAST RECORD NO: ":LRNM
2640 FOR I=FRNM TO LRNM
2650 CALL CLEAR
2660 CALL VCHAR(1,1,136,24)
2670 CALL VCHAR(1,2,136,24)
2680 CALL VCHAR(1,31,136,24)
2690 CALL VCHAR(1,32,136,24)
2700 R=I
2710 INPUT "AUTHOR #1(14): ":AT1$
2715 IF AT1$="" THEN 410
2720 INPUT "AUTHOR #2: ":AT2$
2730 INPUT "AUTHOR #3: ":AT3$
2740 INPUT "TITLE(39): ":TT$
2750 INPUT "SUBJECT(3): ":SBJ$
2760 INPUT "NOTES(9): ":NT$
2770 CTT$=SEG$(TT$,1,27)
2780 Q=R-1
2790 OPEN #2:"DSK1.LIBCAT",RELATIVE,INTERNAL,INPUT ,FIXED 108
2800 FOR J=1 TO Q
2810 !*** CHECKING FOR THE SAME TITLE IN THE FILE ******
2820 INPUT #2,REC J:CA1$,CA2$,CA3$,CT$,CSB$,CNT$
2830 CTSG$=SEG$(CT$,1,27)
2840 IF CTSG$=CTT$ THEN CLOSE #2 :: PRINT "TITLE ALREADY ENTERED": : :: GOTO 2710
2850 NEXT J
2860 CLOSE #2
2870 OPEN #1:"DSK1.LIBCAT",RELATIVE,INTERNAL,UPDATE,FIXED 108
2880 PRINT #1,REC R:AT1$,AT2$,AT3$,TT$,SBJ$,NT$
2890 CLOSE #1
2900 NEXT I
2910 CALL CLEAR
2920 INPUT "MORE RECORDS?(Y/N)  :":MRECS$
2930 IF MRECS$="N" THEN 440
2940 IF MRECS$="Y" THEN 2570
2950 GOTO 2910
2960 !********************
2970 !*** QUIT ROUTINE ***
2980 !********************
2990 CALL CLEAR
3000 DISPLAY AT(9,5):"LEAVING LIBRARY MASTER 3.1.2":", RETURNING TO EXT. BASIC.": : : :"THANKS FOR USING THE        APPLICATION."
3010 FOR I=1 TO 500
3020 NEXT I
3030 CALL CLEAR :: END
3040 !**********************
3050 !** CLEAR SUB ROUTINE *
3060 !**********************
3070 FOR DEL=11 TO 20
3080 CALL HCHAR(DEL,3,32,28)
3090 NEXT DEL
3100 RETURN
