100 !********************
110 !***    CALC-DH   ***
120 !*** A HEX TO DEC ***
130 !*** & DEC TO HEX ***
140 !***  CONVERTER   ***
150 !***  VERSION 1.0 ***
160 !***  BY R. LOCK  ***
170 !***  JUNE 2014   ***
180 !********************
190 REM ON WARNING NEXT
200 DIM T(16),BN16$(16),HX$(4),BN4$(4),CF(4)
210 !*********************
220 !** BUILD 2^X ARRAY **
230 !*********************
240 FOR E=0 TO 15 :: T(E)=2^E :: NEXT E
250 !**********************
260 !*** COLORS/DISPLAY ***
270 !**********************
280 CALL CHAR(128,"0000FFFF00000000")
290 CALL SCREEN(5):: CALL COLOR(0,5,1)
300 FOR CS=1 TO 12 :: CALL COLOR(CS,15,2):: NEXT CS :: CALL COLOR(13,12,2)
310 !**********************
320 !***  CALC-DH MENU  ***
330 !**********************
340 CALL CLEAR :: DISPLAY AT(1,9):"CALC-DH MENU" :: CALL HCHAR(2,11,128,12)
350 DISPLAY AT(5,1):"1.  DEC TO HEX"
360 DISPLAY AT(7,1):"2.  HEX TO DEC"
370 DISPLAY AT(9,1):"3.  QUIT TO XB"
380 CALL KEY(0,K,S):: IF S=0 THEN 380
390 IF K=49 THEN 430
400 IF K=50 THEN 1010
410 IF K=51 THEN 1500
420 IF K<49 OR K>51 THEN 380
430 !********************
440 !***  DEC TO HEX  ***
450 !********************
460 CALL CLEAR
470 DISPLAY AT(1,10):"DEC TO HEX" :: CALL HCHAR(2,12,128,10)
480 DISPLAY AT(5,1):"DEC VAL:"
490 DISPLAY AT(7,1):"HEX VAL:"
500 DISPLAY AT(9,1):"BIN VAL:"
510 !**********************
520 !***  ACCEPT INPUT  ***
530 !**********************
540 ACCEPT AT(5,10)VALIDATE(DIGIT)BEEP SIZE(5):DEC$ :: IF DEC$="" THEN 930
550 DEC=VAL(DEC$)
560 IF DEC<0 OR DEC>65535 THEN 540
570 !*******************
580 !** RESETTING BIN **
590 !*******************
600 FOR E=15 TO 0 STEP -1 :: BN16$(E)="0" :: NEXT E
610 !******************
620 !** BUILDING BIN **
630 !******************
640 BIN$="" :: FOR E=15 TO 0 STEP -1
650 IF DEC>=T(E)THEN BN16$(E)="1" :: DEC=DEC-T(E)
660 BIN$=BIN$&BN16$(E):: NEXT E
670 !*********************
680 !** CALCULATING HEX **
690 !*********************
700 HEX$="" :: FOR E=3 TO 0 STEP -1
710 IF SEG$(BIN$,-4*E+13,4)="1111" THEN HX$(E)="F"
720 IF SEG$(BIN$,-4*E+13,4)="1110" THEN HX$(E)="E"
730 IF SEG$(BIN$,-4*E+13,4)="1101" THEN HX$(E)="D"
740 IF SEG$(BIN$,-4*E+13,4)="1100" THEN HX$(E)="C"
750 IF SEG$(BIN$,-4*E+13,4)="1011" THEN HX$(E)="B"
760 IF SEG$(BIN$,-4*E+13,4)="1010" THEN HX$(E)="A"
770 IF SEG$(BIN$,-4*E+13,4)="1001" THEN HX$(E)="9"
780 IF SEG$(BIN$,-4*E+13,4)="1000" THEN HX$(E)="8"
790 IF SEG$(BIN$,-4*E+13,4)="0111" THEN HX$(E)="7"
800 IF SEG$(BIN$,-4*E+13,4)="0110" THEN HX$(E)="6"
810 IF SEG$(BIN$,-4*E+13,4)="0101" THEN HX$(E)="5"
820 IF SEG$(BIN$,-4*E+13,4)="0100" THEN HX$(E)="4"
830 IF SEG$(BIN$,-4*E+13,4)="0011" THEN HX$(E)="3"
840 IF SEG$(BIN$,-4*E+13,4)="0010" THEN HX$(E)="2"
850 IF SEG$(BIN$,-4*E+13,4)="0001" THEN HX$(E)="1"
860 IF SEG$(BIN$,-4*E+13,4)="0000" THEN HX$(E)="0"
870 HEX$=HEX$&HX$(E):: NEXT E
880 !*********************
890 !***  DISPLAY HEX  ***
900 !*********************
910 DISPLAY AT(7,10):HEX$
920 DISPLAY AT(9,10):BIN$
930 DISPLAY AT(20,2):"PRESS ANY KEY TO CONTINUE"
940 DISPLAY AT(21,3):"OR PRESS 'BACK' TO MENU"
950 CALL KEY(0,K,S):: IF S=0 THEN 950
960 IF K=15 THEN 310
970 CALL HCHAR(7,11,32,12)
980 CALL HCHAR(9,11,32,18)
990 CALL HCHAR(20,1,32,64)
1000 GOTO 510
1010 !********************
1020 !***  HEX TO DEC  ***
1030 !********************
1040 CALL CLEAR
1050 DISPLAY AT(1,10):"HEX TO DEC" :: CALL HCHAR(2,12,128,10)
1060 DISPLAY AT(5,1):"HEX VAL:"
1070 DISPLAY AT(7,1):"DEC VAL:"
1080 DISPLAY AT(9,1):"BIN VAL:"
1090 !**********************
1100 !***  ACCEPT INPUT  ***
1110 !**********************
1120 ACCEPT AT(5,10)VALIDATE(DIGIT,"ABCDEF")BEEP SIZE(4):HEX$
1130 IF HEX$="" THEN 1420
1140 FOR E=0 TO 3 :: HX$(E)=SEG$(HEX$,-1*E+4,1):: NEXT E
1150 !*********************
1160 !*** TESTING INPUT ***
1170 !*********************
1180 BIN$="" :: DEC=0 :: FOR E=0 TO 3
1190 IF HX$(E)="F" THEN CF(E)=15 :: BN4$(E)="1111"
1200 IF HX$(E)="E" THEN CF(E)=14 :: BN4$(E)="1110"
1210 IF HX$(E)="D" THEN CF(E)=13 :: BN4$(E)="1101"
1220 IF HX$(E)="C" THEN CF(E)=12 :: BN4$(E)="1100"
1230 IF HX$(E)="B" THEN CF(E)=11 :: BN4$(E)="1011"
1240 IF HX$(E)="A" THEN CF(E)=10 :: BN4$(E)="1010"
1250 IF HX$(E)="9" THEN CF(E)=9 :: BN4$(E)="1001"
1260 IF HX$(E)="8" THEN CF(E)=8 :: BN4$(E)="1000"
1270 IF HX$(E)="7" THEN CF(E)=7 :: BN4$(E)="0111"
1280 IF HX$(E)="6" THEN CF(E)=6 :: BN4$(E)="0110"
1290 IF HX$(E)="5" THEN CF(E)=5 :: BN4$(E)="0101"
1300 IF HX$(E)="4" THEN CF(E)=4 :: BN4$(E)="0100"
1310 IF HX$(E)="3" THEN CF(E)=3 :: BN4$(E)="0011"
1320 IF HX$(E)="2" THEN CF(E)=2 :: BN4$(E)="0010"
1330 IF HX$(E)="1" THEN CF(E)=1 :: BN4$(E)="0001"
1340 IF HX$(E)="0" THEN CF(E)=0 :: BN4$(E)="0000"
1350 DEC=DEC+CF(E)*16^E :: NEXT E
1360 FOR E=3 TO 0 STEP -1 :: BIN$=BIN$&BN4$(E):: NEXT E
1370 !*********************
1380 !*** CALCULATE DEC ***
1390 !*********************
1400 DISPLAY AT(7,9):DEC
1410 DISPLAY AT(9,10):BIN$
1420 DISPLAY AT(20,2):"PRESS ANY KEY TO CONTINUE"
1430 DISPLAY AT(21,3):"OR PRESS 'BACK' TO MENU"
1440 CALL KEY(0,K,S):: IF S=0 THEN 1440
1450 IF K=15 THEN 310
1460 CALL HCHAR(7,11,32,12)
1470 CALL HCHAR(9,11,32,18)
1480 CALL HCHAR(20,1,32,64)
1490 GOTO 1090
1500 !****************
1510 !**  QUIT PROG **
1520 !****************
1530 CALL CLEAR :: END

