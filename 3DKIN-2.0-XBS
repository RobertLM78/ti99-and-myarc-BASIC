100 !********************
110 !* 3D STRAIGHT LINE *
120 !**   KINEMATICS   **
130 !**  VERSION 2.0   **
140 !**   BY R. LOCK   **
150 !**   JUNE 2014    **
160 !* LICENSE: GPL 3.0 *
170 !********************
180 CALL CLEAR :: CALL SCREEN(5):: T0=0 :: TF=1 :: ON WARNING NEXT
190 !*****************
200 !** SET DISPLAY **
210 !*****************
220 FOR I=0 TO 12 :: CALL COLOR(I,16,2):: NEXT I
230 KIN$="STRAIGHT LINE KINEMATICS"
240 U$="0000FFFF00000000" :: CALL COLOR(13,11,2):: CALL CHAR(128,U$)
250 DISPLAY AT(1,2):KIN$ :: CALL HCHAR(2,4,128,24)
260 !*****************
270 !** SET STRINGS **
280 !*****************
290 PARA$="SET PARAMETERS" :: TINT$="SET TIME INTERVAL" :: EVAL$="EVALUATE"
300 !** PARAMETER STRINGS **
310 X0$="INITIAL X POS." :: Y0$="INITIAL Y POS." :: Z0$="INITIAL Z POS."
320 VX0$="INITIAL X VEL." :: VY0$="INITIAL Y VEL." :: VZ0$="INITIAL Z VEL."
330 AX$="X ACCELERATION" :: AY$="Y ACCELERATION" :: AZ$="Z ACCELERATION"
340 !** T-INTERVAL STRINGS *
350 T0$="INITIAL TIME" :: TF$="FINAL TIME"
360 !** EVAL. STRINGS **
370 RS0$="INITIAL POSITION" :: RSF$="FINAL POSITION"
380 VS0$="INITIAL VELOCITY" :: VSF$="FINAL VELOCITY"
390 AS$="ACCELERATION"
400 !*****************
410 !**  MAIN MENU  **
420 !*****************
430 DISPLAY AT(3,10):"MAIN MENU"
440 DISPLAY AT(5,4):"1. "&PARA$&""
450 DISPLAY AT(7,4):"2. "&TINT$&""
460 DISPLAY AT(9,4):"3. "&EVAL$&""
470 DISPLAY AT(11,4):"4. VIEW "&PARA$&""
480 DISPLAY AT(13,4):"Q. QUIT TO XB"
490 DISPLAY AT(19,1):"USE SI UNITS (m,s,m/s,m/s^2)"
500 CALL KEY(0,K,S):: IF S=0 THEN 500
510 IF K=49 THEN 580
520 IF K=50 THEN 850
530 IF K=51 THEN 1000
540 IF K=52 THEN 1570
550 IF K=81 THEN 1730
560 IF K<50 THEN 500 :: IF K>81 THEN 500 :: IF K>52 THEN 500
570 IF S<>-1 THEN 500
580 !**********************
590 !** INPUT PARAMETERS **
600 !**********************
610 CALL HCHAR(3,1,32,704)
620 DISPLAY AT(3,7):"ENTER PARAMETERS"
630 DISPLAY AT(5,1):"ENTER "&X0$&":"
640 DISPLAY AT(7,1):"ENTER "&Y0$&":"
650 DISPLAY AT(9,1):"ENTER "&Z0$&":"
660 DISPLAY AT(11,1):"ENTER "&VX0$&":"
670 DISPLAY AT(13,1):"ENTER "&VY0$&":"
680 DISPLAY AT(15,1):"ENTER "&VZ0$&":"
690 DISPLAY AT(17,1):"ENTER "&AX$&":"
700 DISPLAY AT(19,1):"ENTER "&AY$&":"
710 DISPLAY AT(21,1):"ENTER "&AZ$&":"
720 DISPLAY AT(23,2):"INITIAL VALUES IMPLY T0=0"
730 ACCEPT AT(6,4)VALIDATE(NUMERIC)BEEP:X0 :: IF LEN(STR$(X0))=0 THEN 730
740 ACCEPT AT(8,4)VALIDATE(NUMERIC)BEEP:Y0 :: IF LEN(STR$(Y0))=0 THEN 740
750 ACCEPT AT(10,4)VALIDATE(NUMERIC)BEEP:Z0 :: IF LEN(STR$(Z0))=0 THEN 750
760 ACCEPT AT(12,4)VALIDATE(NUMERIC)BEEP:VX0 :: IF LEN(STR$(VX0))=0 THEN 760
770 ACCEPT AT(14,4)VALIDATE(NUMERIC)BEEP:VY0 :: IF LEN(STR$(VY0))=0 THEN 770
780 ACCEPT AT(16,4)VALIDATE(NUMERIC)BEEP:VZ0 :: IF LEN(STR$(VZ0))=0 THEN 780
790 ACCEPT AT(18,4)VALIDATE(NUMERIC)BEEP:AX :: IF LEN(STR$(AX))=0 THEN 790
800 ACCEPT AT(20,4)VALIDATE(NUMERIC)BEEP:AY :: IF LEN(STR$(AY))=0 THEN 800
810 ACCEPT AT(22,4)VALIDATE(NUMERIC)BEEP:AZ :: IF LEN(STR$(AZ))=0 THEN 810
820 DISPLAY AT(24,1):"PRESS ANY KEY TO CONTINUE."
830 CALL KEY(O,K,S):: IF S=0 THEN 830
840 CALL HCHAR(3,1,32,704):: GOTO 400
850 !*********************
860 !**  SET TIME INT.  **
870 !*********************
880 CALL HCHAR(3,1,32,704)
890 DISPLAY AT(3,6):"ENTER TIME INTERVAL"
900 DISPLAY AT(5,1):"ENTER "&T0$&":"
910 DISPLAY AT(7,1):"ENTER "&TF$&":"
920 ACCEPT AT(6,4)VALIDATE(NUMERIC)BEEP:T0 :: IF LEN(STR$(T0))=0 THEN 920
930 ACCEPT AT(8,4)VALIDATE(NUMERIC)BEEP:TF :: IF LEN(STR$(TF))=0 THEN 930
940 X0=X0+VX0*T0+0.5*AX*T0^2 !*PRELIMINARY EVALUATIONS *
950 Y0=Y0+VY0*T0+0.5*AY*T0^2 !**  FOR THE CASE T0<>0  **
960 Z0=Z0+VZ0*T0+0.5*AZ*T0^2 !**************************
970 DISPLAY AT(23,1):"PRESS ANY KEY TO CONTINUE."
980 CALL KEY(O,K,S):: IF S=0 THEN 980
990 CALL HCHAR(3,1,32,704):: GOTO 400
1000 !******************
1010 !** EVALUATIONS  **
1020 !******************
1030 !***  PAGE 1    ***
1040 CALL HCHAR(3,1,32,704)
1050 X=X0+VX0*TF+0.5*AX*TF^2
1060 Y=Y0+VY0*TF+0.5*AY*TF^2
1070 Z=Z0+VZ0*TF+0.5*AZ*TF^2
1080 RF=(X^2+Y^2+Z^2)^0.5 :: R0=(X0^2+Y0^2+Z0^2)^0.5
1090 VXF=VX0+AX*TF :: VYF=VY0+AY*TF :: VZF=VZ0+AZ*TF
1100 VF=(VXF^2+VYF^2+VZF^2)^0.5 :: V0=(VX0^2+VY0^2+VZ0^2)^0.5
1110 TINT=TF-T0
1120 DISPLAY AT(3,4):"DISPLAY CALCULATIONS 1"
1130 DISPLAY AT(4,1):"THE TIME INTEVAL IS "&STR$(TINT)&""
1140 DISPLAY AT(5,1):" SECONDS."
1150 DISPLAY AT(7,1):"THE "&RS0$&" IS:"
1160 DISPLAY AT(8,1):" ("&STR$(X0)&","&STR$(Y0)&","&STR$(Z0)&")"
1170 DISPLAY AT(9,1):"MAG.: "&STR$(R0)&" METERS."
1180 DISPLAY AT(10,1):"THE "&RSF$&" IS:"
1190 DISPLAY AT(11,1):" ("&STR$(X)&","&STR$(Y)&","&STR$(Z)&")"
1200 DISPLAY AT(12,1):"MAG.: "&STR$(RF)&" METERS."
1210 DISPLAY AT(14,1):"THE "&VS0$&" IS:"
1220 DISPLAY AT(15,1):" ("&STR$(VX0)&","&STR$(VY0)&","&STR$(VZ0)&")"
1230 DISPLAY AT(16,1):"MAG.: "&STR$(V0)&""
1240 DISPLAY AT(17,1):" METERS/SECOND."
1250 DISPLAY AT(18,1):"THE "&VSF$&" IS:"
1260 DISPLAY AT(19,1):" ("&STR$(VXF)&","&STR$(VYF)&","&STR$(VZF)&")"
1270 DISPLAY AT(20,1):"MAG.: "&STR$(VF)&""
1280 DISPLAY AT(21,1):" METERS/SECOND."
1290 DISPLAY AT(23,1):"PRESS ANY KEY TO CONTINUE."
1300 CALL KEY(O,K,S):: IF S=0 THEN 1300
1310 CALL HCHAR(3,1,32,704)
1320 !******************
1330 !** EVALUATIONS  **
1340 !******************
1350 !***   PAGE 2   ***
1360 AS=(AX^2+AY^2+AZ^2)^0.5
1370 XD=X-X0 :: YD=Y-Y0 :: ZD=Z-Z0 :: DIS=(XD^2+YD^2+ZD^2)^0.5
1380 VAV=DIS/TINT
1390 DISPLAY AT(3,4):"DISPLAY CALCULATIONS 2"
1400 DISPLAY AT(5,1):"THE "&AS$&" IS:"
1410 DISPLAY AT(6,1):" ("&STR$(AX)&","&STR$(AY)&","&STR$(AZ)&")"
1420 DISPLAY AT(7,1):"MAG.: "&STR$(AS)&""
1430 DISPLAY AT(8,1):" METERS/SECOND^2."
1440 DISPLAY AT(10,1):"THE TOTAL DISPLACEMENT IS"
1450 DISPLAY AT(11,1):""&STR$(DIS)&" METERS."
1460 DISPLAY AT(13,1):RS0$
1470 DISPLAY AT(14,1):" R0=("&STR$(X0)&","&STR$(Y0)&","&STR$(Z0)&")"
1480 DISPLAY AT(15,1):RSF$
1490 DISPLAY AT(16,1):" RF=("&STR$(X)&","&STR$(Y)&","&STR$(Z)&")"
1500 DISPLAY AT(18,1):"THE AVERAGE VELOCITY IS:"
1510 DISPLAY AT(19,1):""&STR$(VAV)&" METERS/SECOND."
1520 DISPLAY AT(23,1):"PRESS ANY KEY TO CONTINUE."
1530 DISPLAY AT(22,1):"PRESS 'BACK' TO PREVIOUS OR"
1540 CALL KEY(O,K,S):: IF S=0 THEN 1540
1550 IF K=15 THEN 1000 :: IF K<>15 THEN 1560 :: IF S<>-1 THEN 1540
1560 CALL HCHAR(3,1,32,704):: GOTO 400
1570 !*******************
1580 !** VIEW PARAMTRS **
1590 !*******************
1600 CALL HCHAR(3,1,32,704)
1610 DISPLAY AT(3,7):"VIEW PARAMETERS"
1620 DISPLAY AT(5,1):"THE "&RS0$&" IS:"
1630 DISPLAY AT(6,1):" ("&STR$(X0)&","&STR$(Y0)&","&STR$(Z0)&")"
1640 DISPLAY AT(8,1):"THE "&VS0$&" IS:"
1650 DISPLAY AT(9,1):" ("&STR$(VX0)&","&STR$(VY0)&","&STR$(VZ0)&")"
1660 DISPLAY AT(11,1):"THE "&AS$&" IS:"
1670 DISPLAY AT(12,1):" ("&STR$(AX)&","&STR$(AY)&","&STR$(AZ)&")"
1680 DISPLAY AT(14,1):"THE TIME INTEVAL IS:"
1690 DISPLAY AT(15,1):" ("&STR$(T0)&","&STR$(TF)&")"
1700 DISPLAY AT(23,1):"PRESS ANY KEY TO CONTINUE."
1710 CALL KEY(O,K,S):: IF S=0 THEN 1710
1720 CALL HCHAR(3,1,32,704):: GOTO 400
1730 !*******************
1740 !** EXIT SEQUENCE **
1750 !*******************
1760 CALL CLEAR :: END

