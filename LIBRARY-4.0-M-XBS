100 !*** ROF IS MAX. NO. ***
110 !** SEE LINE #s 320  ***
120 !**  2740 & 2910     *** 
130 !*** RECORDS ON FILE ***
140 !***********************
150 !**   LIBRARY MASTER  **
160 !**  VERSION 4.0-M    **
170 !**     JULY 2014     **
180 !** FOR MYARC XBASIC  **
190 !**  BY ROBERT LOCK   **
200 !**    ROBERTLM78     **
210 !***********************
220 !*********************
230 !***  SET DISPLAY  ***
240 !*********************
245 ON WARNING NEXT
250 CALL CLEAR :: CALL GRAPHICS(1) :: CALL SCREEN(5)
260 FOR CS=1 TO 12 :: CALL COLOR(CS,16,2) :: NEXT CS
270 CALL COLOR(0,5,1) :: CALL COLOR(13,12,2)
280 U$="0000FFFF00000000" :: CALL CHAR(128,U$)
290 !*********************
300 !** INIT STRING VAR.**
310 !*********************
320 DIM ATA$(400,3),TTA$(400),SBJA$(400),NTA$(400),SUBJ$(28),MITEM$(13)
330 !** MAIN MENU ITEMS **
340 !*********************
350 RESTORE 360 :: FOR L=0 TO 8 :: READ MITEM$(L)
360 DATA Library Master Menu,1) By Author,2) By Title,3) Browse Titles
370 DATA 4) By Subject,5) Sort File by Title,6) LOAD/UPDATE/SAVE File
380 DATA 7) Quit to MXB,8) Quit to FWEB
390 NEXT L :: RESTORE 420
400 !*** UPDATE MENU ***
410 FOR L=9 TO 12 :: READ MITEM$(L)
420 DATA 1) LOAD File,2) UPDATE File,3) SAVE File,4) BACK to Main Menu
430 NEXT L :: RESTORE 470
440 !*** SPLASH SCREEN ***
450 !*********************
460 READ LM4$,VER$,BY$,CONT$
470 DATA Library Master,Version 4.0-M,By RobertLM78,Press SPACE to continue
480 RESTORE 490 :: READ MXB$
490 DATA Written for Myarc Ext. Basic
500 !***  HELP ITEMS  ***
510 !********************
520 RESTORE 530 :: READ SGTN$,CHKSP$
530 DATA Suggestions:,-use correct spelling
540 RESTORE 550 :: READ EEIC$,CLRSC$
550 DATA -use the correct criteria for search mode,Press'DEL'to clear screen
560 RESTORE 570 :: READ BK$,AID$
570 DATA Press 'BACK' to search menu,Press 'AID' to view subjects
580 RESTORE 590 :: READ NPTL1$
590 DATA NOTE: Do not use punctuation
600 RESTORE 610 :: READ NPTL2$
610 DATA in title entry and enter as much of title as possible
620 NPTL$=NPTL1$&NPTL2$
630 !*** SUBJECT ITEMS ***
640 !*********************
650 RESTORE 660 :: FOR L=0 TO 27 :: READ SUBJ$(L)
660 DATA Art (ART),Fiction (FIC),Historical Fiction (HF),History (HST)
670 DATA Ireland (IRE),Music (MUS),Non-Fiction (GNF),Politics (POL),Poetry (PTY)
680 DATA Reference (REF),Religion (REL),Science (SCI),Scotland (SCT),Text (TXT)
690 DATA ART,FIC,HF,HST,IRE,MUS,GNF,POL,PTY,REF,REL,SCI,SCT,TXT
700 NEXT L
710 !*** SUBMENU ITEMS ***
720 !*********************
730 RESTORE 740 :: READ AUTHOR$,AN$
740 DATA Search by Author,ENTER AUTHOR'S LAST NAME
750 RESTORE 760 :: READ TITLE$,TTSCH$
760 DATA Search by Title,ENTER TITLE (28 CHARS MAX)
770 RESTORE 780 :: READ SUB$,ESUB$
780 DATA Search by Subject,ENTER SUBJECT
790 RESTORE 800 :: READ UDDF$
800 DATA Load/Update/Save Data File
810 RESTORE 820 :: READ BROWT$,PRNUM$
820 DATA Browse by Title,Press # to view book info
830 RESTORE 840 :: READ SORTT$,BEGST$
840 DATA Sort Data File by Title,Press 'BEGIN' to start sort
850 !*********************
860 !*******************
870 !** SPLASH SCREEN **
880 !*******************
890 DISPLAY AT(7,8):LM4$ :: DISPLAY AT(9,9):VER$
900 DISPLAY AT(11,2):MXB$ :: DISPLAY AT(21,7):BY$ :: DISPLAY AT(23,3):CONT$
910 DISPLAY AT(17,9):<e3>GO:TAB(9);<e3>IF
920 CALL KEY(0,K,S) :: IF S=0 THEN 920
930 IF K=32 THEN 940 :: IF K<>32 THEN 920
940 !******************
950 !*** MAIN MENU ****
960 !******************
970 CALL CLEAR :: DISPLAY AT(3,6):MITEM$(0) :: CALL HCHAR(4,8,128,19)
980 FOR R=1 TO 8 :: DISPLAY AT(2*R+4,4):MITEM$(R) :: NEXT R
990 CALL KEY(0,K,S) :: IF S=0 THEN 990
1000 IF K=49 THEN 1090 !*** 1) BY AUTHOR
1010 IF K=50 THEN 1330 !** 2) BY TITLE
1020 IF K=51 THEN 1560 !** 3) BROWSE TITLE
1030 IF K=52 THEN 1890 !** 4) BY SUBJECT
1040 IF K=53 THEN 2330 !** 5) SORT BY TITLE
1050 IF K=54 THEN 2620 !** 6) LOAD/SAVE FILE
1060 IF K=55 THEN 3490 !** 7) QUIT TO MXB
1070 IF K=56 THEN 3570 !** 8) QUIT TO FWEB
1080 IF K<49 OR K>56 THEN 990
1090 !*********************
1100 !*** AUTHOR SEARCH ***
1110 !*********************
1120 CALL CLEAR :: DISPLAY AT(3,9):AUTHOR$ :: CALL HCHAR(4,11,128,16)
1130 DISPLAY AT(7,3):AN$ :: DISPLAY AT(11,4):SGTN$ :: DISPLAY AT(13,1):CHKSP$
1140 DISPLAY AT(14,1):EEIC$ :: DISPLAY AT(21,3):CLRSC$ :: DISPLAY AT(23,2):BK$
1150 CALL KEY(0,K,S) :: IF S=0 THEN 1150
1160 IF K=15 THEN 940 !**  'BACK'  **
1170 IF K=3 THEN GOSUB 3670 !** 'DEL' (SUB CLR) **
1180 ACCEPT AT(9,3):SA$
1190 IF LEN(SA$)=0 THEN 1150
1200 DISPLAY AT(20,1):"Current REC:"
1210 FOR I=1 TO ROF :: DISPLAY AT(20,14):I
1220 IF ATA$(I,0)=SA$ THEN GOSUB 1260
1230 IF ATA$(I,1)=SA$ THEN GOSUB 1260
1240 IF ATA$(I,2)=SA$ THEN GOSUB 1260
1250 NEXT I :: GOTO 1150
1260 DISPLAY AT(11,1):ATA$(I,0):ATA$(I,1):ATA$(I,2): :TTA$(I):SBJA$(I):NTA$(I)
1270 DISPLAY AT(20,22):"REC";I
1280 CALL KEY(0,K,S) :: IF S=0 THEN 1280
1290 IF K=12 THEN RETURN !** 'PROC'D' **
1300 IF K=15 THEN 940 !**  'BACK'  **
1310 IF K=14 THEN 1150 !**  'BEGIN' **  
1320 GOTO 1280
1330 !********************
1340 !*** TITLE SEARCH ***
1350 !********************
1360 CALL CLEAR :: DISPLAY AT(3,7):TITLE$ :: CALL HCHAR(4,9,128,15)
1370 DISPLAY AT(7,3):TTSCH$ :: DISPLAY AT(21,3):CLRSC$ :: DISPLAY AT(23,2):BK$
1380 DISPLAY AT(11,1):NPTL$ :: DISPLAY AT(12,2):CONT$
1390 CALL KEY(0,P,R) :: IF R=0 THEN 1390
1400 IF P=32 THEN GOSUB 3670 :: GOTO 1420
1410 IF P<>32 THEN 1390
1420 DISPLAY AT(11,4):SGTN$ :: DISPLAY AT(13,1):CHKSP$:EEIC$
1430 CALL KEY(0,K,S) :: IF S=0 THEN 1430
1440 IF K=15 THEN 940
1450 IF K=3 THEN GOSUB 3670
1460 ACCEPT AT(9,1):TS$ :: IF LEN(TS$)=0 THEN 1430
1470 STS$=SEG$(TS$,1,27)
1480 FOR I=0 TO ROF
1490 DISPLAY AT(20,1):"Current REC:";I
1500 CTTS$=SEG$(TTA$(I),1,27)
1510 IF CTTS$=STS$ THEN 1530
1520 NEXT I :: GOTO 1430
1530 DISPLAY AT(11,1):ATA$(I,0):ATA$(I,1):ATA$(I,2): :TTA$(I):SBJA$(I):NTA$(I)
1540 DISPLAY AT(20,22):"REC";I
1550 GOTO 1430
1560 !********************
1570 !*** TITLE BROWSE ***
1580 !********************
1590 CALL CLEAR :: DISPLAY AT(3,7):BROWT$ :: CALL HCHAR(4,9,128,15)
1600 DISPLAY AT(21,3):PRNUM$
1610 DISPLAY AT(23,2):CONT$ :: DISPLAY AT(24,2):BK$
1620 R=0 :: PAGE=1 :: FOR I=1 TO ROF :: R=R+1
1630 CALL VCHAR(2*R+4,1,ASC(STR$(R)),1) :: CALL VCHAR(2*R+4,2,ASC("."),1)
1640 DISPLAY AT(2*R+4,1):TTA$(I) :: DISPLAY AT(20,1):"PAGE:";PAGE
1650 IF R=7 THEN GOSUB 1720
1660 NEXT I
1670 CALL KEY(0,K,S) :: IF S=0 THEN 1670
1680 IF K>=49 AND K<=55 THEN 1710
1690 IF K=15 THEN 940
1700 GOTO 1670
1710 T=I+R :: GOSUB 1800 :: GOTO 1670
1720 !*** RESET ROW  ***
1730 !******************
1740 R=0 :: T=I :: PAGE=PAGE+1
1750 CALL KEY(0,K,S) :: IF S=0 THEN 1750
1760 IF K>=49 AND K<=55 THEN 1800
1770 IF K=32 THEN GOSUB 3700 :: RETURN
1780 IF K=15 THEN 940
1790 GOTO 1750
1800 !*** DISPLAY DETAILS ***
1810 !***********************
1820 P=VAL(CHR$(K))-7 :: T=T+P :: GOSUB 3700
1830 DISPLAY AT(11,1):ATA$(T,0):ATA$(T,1):ATA$(T,2): :TTA$(T):SBJA$(T):NTA$(T)
1840 CALL KEY(O,K,S) :: IF S=0 THEN 1840
1850 IF K=32 THEN GOSUB 3700 :: RETURN
1860 IF K=15 THEN 940
1870 GOTO 1840
1880 !** GOSUB END **
1890 !**********************
1900 !*** SUBJECT SEARCH ***
1910 !**********************
1920 CALL CLEAR :: DISPLAY AT(3,7):SUB$ :: CALL HCHAR(4,9,128,17)
1930 DISPLAY AT(7,9):ESUB$ :: DISPLAY AT(21,3):CLRSC$ :: DISPLAY AT(23,2):BK$
1940 DISPLAY AT(19,1):AID$
1950 DISPLAY AT(11,4):SGTN$ :: DISPLAY AT(13,1):CHKSP$:EEIC$
1960 CALL KEY(0,K,S) :: IF S=0 THEN 1960
1970 IF K=15 THEN 940
1980 IF K=3 THEN GOSUB 3670
1990 IF K=1 THEN 2050
2000 ACCEPT AT(9,3):BS$
2010 FOR I=14 TO 27
2020 IF BS$=SUBJ$(I) THEN 2210
2030 NEXT I
2040 GOTO 1960
2050 GOSUB 3670
2060 DISPLAY AT(11,1):"Use arrow keys to navigate list.  Press'PROC'D'to cont"
2070 CALL KEY(0,PR,PRD) :: IF PRD=0 THEN 2070
2080 IF PR=12 THEN 2100
2090 IF PR<>12 THEN 2070
2100 CALL KEY(0,SB,SL) :: IF SL=0 THEN 2100
2110 IF SB=11 THEN 2150
2120 IF SB=10 THEN 2180
2130 IF SB=14 THEN GOSUB 3670 :: GOTO 1960
2140 GOTO 2100
2150 R=10 :: FOR I=14 TO 20 :: R=R+1
2160 DISPLAY AT(R,1):SUBJ$(I) :: NEXT I
2170 GOTO 2100
2180 R=10 :: FOR I=21 TO 27 :: R=R+1
2190 DISPLAY AT(R,1):SUBJ$(I) :: NEXT I
2200 GOTO 2100
2210 FOR I=1 TO ROF
2220 DISPLAY AT(20,1):"Current REC:";I
2230 IF SBJA$(I)=BS$ THEN GOSUB 2260
2240 NEXT I
2250 GOTO 1960
2260 DISPLAY AT(11,1):ATA$(I,0):ATA$(I,1):ATA$(I,2): :TTA$(I):SBJA$(I):NTA$(I)
2270 DISPLAY AT(20,22):"REC";I
2280 CALL KEY(0,K,S)
2290 IF K=12 THEN RETURN
2300 IF K=15 THEN 940
2310 IF K=14 THEN 1960
2320 GOTO 2280
2330 !********************
2340 !** SORT BY TITLE  **
2350 !********************
2360 CALL CLEAR :: DISPLAY AT(3,3):SORTT$ :: CALL HCHAR(4,5,128,23)
2370 DISPLAY AT(11,3):BEGST$ :: DISPLAY AT(23,2):BK$
2380 CALL KEY(0,K,S) :: IF S=0 THEN 2380
2390 IF K=14 THEN 2410
2395 IF K=15 THEN 940
2400 GOTO 2380
2410 GOSUB 3670 :: DISPLAY AT(11,1):"Sorting up to REC#:"
2420 DISPLAY AT(13,1):"Comparing title:"
2430 P=ROF
2440 IF P<=1 THEN 940
2450 P=INT(P/2) :: M=ROF-P
2460 DISPLAY AT(11,20):M
2470 SS=0
2480 FOR J=1 TO M
2490 DISPLAY AT(14,1):TTA$(J)&"              " :: DISPLAY AT(11,25):J
2500 X=J+P
2510 IF TTA$(J)>TTA$(X) THEN GOSUB 2550
2520 NEXT J
2530 IF SS>0 THEN 2470
2540 GOTO 2440
2550 TMPA0$=ATA$(J,0) :: TMPA1$=ATA$(J,1) :: TMPA2$=ATA$(J,2)
2560 TMPTIT$=TTA$(J) :: TMPSBJ$=SBJA$(J) :: TMPNT$=NTA$(J)
2570 ATA$(J,0)=ATA$(X,0) :: ATA$(J,1)=ATA$(X,1) :: ATA$(J,2)=ATA$(X,2)
2580 TTA$(J)=TTA$(X) :: SBJA$(J)=SBJA$(X) :: NTA$(J)=NTA$(X)
2590 ATA$(X,0)=TMPA0$ :: ATA$(X,1)=TMPA1$ :: ATA$(X,2)=TMPA2$
2600 TTA$(X)=TMPTIT$ :: SBJA$(X)=TMPSBJ$ :: NTA$(X)=TMPNT$
2610 SS=1 :: RETURN
2620 !*********************
2630 !** L/S/UPDATE FILE **
2640 !*********************
2650 CALL CLEAR :: DISPLAY AT(3,2):UDDF$ :: CALL HCHAR(4,4,128,26)
2660 FOR R=1 TO 4 :: DISPLAY AT(2*R+4,5):MITEM$(R+8) :: NEXT R
2670 CALL KEY(0,K,S) :: IF S=0 THEN 2670
2680 IF K=49 THEN 2730 !*** 1) LOAD FILE
2690 IF K=50 THEN 3050 !** 2) UPDATE FILE
2700 IF K=51 THEN 2890 !** 3) SAVE FILE
2710 IF K=52 THEN 940 !** 4) BACK to Main Menu
2720 GOTO 2670
2730 !*** LOAD FILE ***
2740 !*****************
2750 OPEN #2:"HDS1.XBASIC.LIB_MSTR.LIBCAT",RELATIVE,INTERNAL,INPUT,FIXED 108
2760 INPUT #2,REC 0:ROF$ :: CLOSE #2
2770 ROF=VAL(ROF$)
2780 GOSUB 3730 :: DISPLAY AT(11,1):"LOADING FILE"
2790 OPEN #2:"HDS1.XBASIC.LIB_MSTR.LIBCAT",RELATIVE,INTERNAL,INPUT,FIXED 108
2800 FOR I=1 TO ROF
2810 DISPLAY AT(14,1):I
2820 INPUT #2,REC I:ATA$(I,0),ATA$(I,1),ATA$(I,2),TTA$(I),SBJA$(I),NTA$(I)
2830 NEXT I :: CLOSE #2
2840 DISPLAY AT(15,1):"RECORDS LOADED:";I-1
2850 DISPLAY AT(17,8):"Press 'BACK'"
2860 CALL KEY(0,K,S) :: IF S=0 THEN 2850
2870 IF K=15 THEN GOSUB 3730 :: GOTO 2660
2880 GOTO 2860
2890 !*** SAVE FILE ***
2900 !*****************
2910 OPEN #2:"HDS1.XBASIC.LIB_MSTR.LIBCAT",RELATIVE,INTERNAL,OUTPUT,FIXED 108
2920 ROF$=STR$(ROF)
2930 PRINT #2,REC 0:ROF$ :: CLOSE #2
2940 GOSUB 3730 :: DISPLAY AT(11,1):"SAVING FILE"
2950 OPEN #2:"HDS1.XBASIC.LIB_MSTR.LIBCAT",RELATIVE,INTERNAL,OUTPUT,FIXED 108
2960 FOR I=1 TO ROF
2970 DISPLAY AT(14,1):I
2980 PRINT #2,REC I:ATA$(I,0),ATA$(I,1),ATA$(I,2),TTA$(I),SBJA$(I),NTA$(I)
2990 NEXT I :: CLOSE #2
3000 DISPLAY AT(15,1):"RECORDS SAVED:";I-1
3010 DISPLAY AT(17,8):"Press 'BACK'"
3020 CALL KEY(0,K,S) :: IF S=0 THEN 3020
3030 IF K=15 THEN GOSUB 3730 :: GOTO 2660
3040 GOTO 3020
3050 !** UPDATE DATA FILE **
3060 !**********************
3070 GOSUB 3730 :: DISPLAY AT(11,1):"PASSWORD:"
3080 ACCEPT AT(12,1):PW$
3090 !*** PASSWORD ***
3100 IF PW$="ZXC420" THEN 3130
3110 !****************
3120 GOTO 3080
3130 GOSUB 3730 :: DISPLAY AT(8,1):"NEXT RECORD #: ";ROF+1
3140 DISPLAY AT(11,1):"ENTER FIRST RECORD NO:"
3150 ACCEPT AT(12,1) VALIDATE(DIGIT):FRNM
3160 DISPLAY AT(13,1):"ENTER LAST RECORD NO:"
3170 ACCEPT AT(14,1) VALIDATE(DIGIT):LRNM
3180 FOR I=FRNM TO LRNM
3190 GOSUB 3730
3200 R=I
3210 DISPLAY AT(6,1):"AUTHOR #1(14):"
3220 DISPLAY AT(8,1):"AUTHOR #2:"
3230 DISPLAY AT(10,1):"AUTHOR #3:"
3240 DISPLAY AT(12,1):"TITLE(39):"
3250 DISPLAY AT(15,1):"SUBJECT(3):"
3260 DISPLAY AT(17,1):"NOTES(9):"
3270 ACCEPT AT(7,1) SIZE(14):ATA$(I,0)
3280 ACCEPT AT(9,1) SIZE(14):ATA$(I,1)
3290 ACCEPT AT(11,1) SIZE(14):ATA$(I,2)
3300 ACCEPT AT(13,1):T1$ :: ACCEPT AT(14,1) SIZE(11):T2$
3310 TTA$(I)=T1$&T2$
3320 ACCEPT AT(16,1) SIZE(3):SBJA$(I)
3330 ACCEPT AT(18,1) SIZE(9):NTA$(I)
3340 CTT$=SEG$(TTA$(I),1,27)
3350 Q=R-1
3360 FOR J=1 TO Q
3370 !*** CHECKING FOR THE SAME TITLE IN THE FILE ******
3380 CTSG$=SEG$(TTA$(J),1,27)
3390 IF CTSG$=CTT$ THEN DISPLAY AT(19,1):"TITLE ALREADY ENTERED" :: GOTO 3270
3400 NEXT J
3410 NEXT I :: I=I-1
3420 IF I>ROF THEN ROF=I
3430 GOSUB 3730
3440 DISPLAY AT(19,1):"MORE RECORDS?(Y/N)"
3450 CALL KEY(O,K,S) :: IF S=0 THEN 3450
3460 IF K=89 THEN 3130
3470 IF K=78 THEN GOSUB 3730 :: GOTO 2660
3480 GOTO 3450
3490 !********************
3500 !*** QUIT ROUTINE ***
3510 !********************
3520 CALL CLEAR
3530 DISPLAY AT(9,5):"Leaving Library Master 4.0-M":", returning to MXB."
3540 FOR I=1 TO 400
3550 NEXT I
3560 CALL CLEAR :: CALL GRAPHICS(4) :: CALL SCREEN(5) :: END
3570 !**********************
3580 !**  LOAD FUNNELWEB  **
3590 !**********************
3600 CALL CLEAR
3610 DISPLAY AT(8,1):"PRESS ANY KEY WHEN READY..."
3620 CALL KEY(0,K,S) :: IF S=0 THEN 3620
3630 RUN "HDS1.FWB.LOAD"
3640 !**************
3650 !***  SUBS  ***
3660 !**************
3670 !SUB CLR
3680 CALL HCHAR(11,1,32,320)
3690 RETURN
3700 !SUB CLRB
3710 CALL HCHAR(6,1,32,448)
3720 RETURN
3730 !SUB CLRU
3740 CALL HCHAR(6,1,32,544)
3750 RETURN

