100 !*****  FILE CONVERTER  *****
110 !* CONVERTS TI-BASIC LIBMSTR*
120 !*    LIBCAT TO READABLE    *
130 !* TO READABLE FORMAT THAT  *
140 !*  CAN BE IMPORTED INTO    *
150 !* MODERN SYSTEMS FOR X86   *
160 !* VERSIONS OF LIB-MASTER   *
170 !* AUTHOR: ROBERTLM78       *
180 !* LICENSE: GPL V 3.0       *
190 !****************************
200 CALL CLEAR
210 DIM ATA$(400,3),TTA$(400),SBJA$(400),NTA$(400),SUBJ$(28)
220 OPEN #2:"HDS1.XBASIC.LIB_MSTR.LIBCAT",RELATIVE,INTERNAL,INPUT,FIXED 108
230 INPUT #2,REC 0:ROF$
240 CLOSE #2
250 ROF=VAL(ROF$)
260 OPEN #2:"HDS1.XBASIC.LIB_MSTR.LIBCAT",RELATIVE,INTERNAL,INPUT,FIXED 108
270 OPEN #3:"HDS1.XBASIC.LIB_MSTR.CATDISP",SEQUENTIAL,OUTPUT,DISPLAY,VARIABLE 80
280 FOR I=1 TO ROF
290 PRINT I;
300 INPUT #2,REC I:ATA$(I,0),ATA$(I,1),ATA$(I,2),TTA$(I),SBJA$(I),NTA$(I)
310 IF ATA$(I,0)="" THEN ATA$(I,0)=" "
320 IF ATA$(I,1)="" THEN ATA$(I,1)=" "
330 IF ATA$(I,2)="" THEN ATA$(I,2)=" "
340 IF SBJA$(I)="" THEN SBJA$(I)=" "
350 IF NTA$(I)="" THEN NTA$(I)=" "
360 CAT1$=TTA$(I)&":"&ATA$(I,0)&":"&ATA$(I,1)&":"
370 CAT2$=ATA$(I,2)&":"&SBJA$(I)&":"&NTA$(I)&":;"&STR$(I)
380 CAT$=CAT1$&CAT2$
390 PRINT #3:CAT$
400 NEXT I
410 CLOSE #2
420 CLOSE #3
430 PRINT
440 PRINT "FILE CONVERTED. "
450 END
